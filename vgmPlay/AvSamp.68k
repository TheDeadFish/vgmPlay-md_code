****************************************************************************
*
*	Average sample rate dac decoder for vgm play V3.10,  
*	file version : 	0.01
* 	created : 		13/01/10
*	modified :		13/12/12
*
****************************************************************************

;count 	   EQUR 0
sub_samp   EQU 1

DAC_WRITE_AVSAMP: ; 54 clocks from start
	move.l #$A04000,a0							;[12]
	move.b #$2A,(a0)+							;[12]
	move.b (VGM_DATA)+,(a7)						;[12]
	moveq #0,d1									;[ 4]
	sub.b (VGM_DATA)+,d1						;[ 8]
	and.w #$0f,d0								;[ 8]
	; 110 clocks from start
	move.b (DAC_SAMPLES)+,(a0)					;[12]
	move.b d1,sub_samp(a7)						;[12]
	lsr.w #1,d1									;[ 8]
	lea DAC_WRITE_AVSAMP_L(pc),DELAY_RET		;[ 8]
	addq.l #1,CTRL_DELAY						;[ 8]
	addq.w #1,SAMPLE_LAG						;[ 4]
	sub.w d0,SAMPLE_LAG							;[ 4]
	bcs.s DAC_WRITE_AVSAMP_DELAY				;[ 8]
	
DAC_WRITE_AVSAMP_L: ; 0 clocks from loop start
	moveq #12,d1								;[ 4]
	lsr.w d1,d1									;[30]
	moveq #15,d0								;[ 4]
	and.b -3(VGM_DATA),d0						;[12]
	move.b -1(VGM_DATA),d1						;[12]
	sub.b d1,sub_samp(a7)						;[16]
	moveq #0,d1									;[ 4]
	addx.w d1,d0								;[ 4]
	lea DAC_WRITE_AVSAMP_RET(pc),DELAY_RET		;[ 8]
	addq.l #1,CTRL_DELAY						;[ 8]
	; 102 clocks from start, equivalent to
	; move.b (DAC_SAMPLES)+,(a0) at 110 clocks 	
	move.b (DAC_SAMPLES)+,$A04001				;[20]
	addq.w #1,SAMPLE_LAG						;[ 4]
	sub.w d0,SAMPLE_LAG							;[ 4]
	bcs.s DAC_WRITE_AVSAMP_DELAY				;[ 8]
DAC_WRITE_AVSAMP_RET: ; 138 clocks from loop start
	subq.b #1,(a7) ; count						;[12]
	bne.s DAC_WRITE_AVSAMP_LC					;[ 8]
	lea PLAY_LOOP(pc),DELAY_RET					;[ 8]
	jmp (DELAY_RET)								;[ 8]
DAC_WRITE_AVSAMP_LC: ; 160 clocks from loop start
	nop											;[ 4]
	bra.s DAC_WRITE_AVSAMP_L					;[10]
	
; hack bits
DAC_WRITE_AVSAMP_DELAY: 						;[ 2]
	bra DELAY_CODE_DAC							;[10]
